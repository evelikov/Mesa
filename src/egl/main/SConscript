#######################################################################
# SConscript for EGL


Import('*')

env = env.Clone()

env.Append(CPPDEFINES = [
    #'_EGL_BUILT_IN_DRIVER_GALLIUM',
    '_EGL_DRIVER_SEARCH_DIR=\\"\\"',
])

if env['platform'] == 'windows':
    env.Append(CPPDEFINES = [
        '_EGL_NATIVE_PLATFORM=_EGL_PLATFORM_WINDOWS',
        '_EGL_OS_WINDOWS',
        '_EGL_GET_CORE_ADDRESSES',
        'KHRONOS_DLL_EXPORTS',
    ])
else:
    env.Append(CPPDEFINES = [
        '_EGL_NATIVE_PLATFORM=_EGL_PLATFORM_X11',
        '_EGL_OS_UNIX',
    ])

env.Append(CPPPATH = [
    '#/include',
])

env.Prepend(LIBS = [
    'egl_dri2',
    libloader,
])

# parse Makefile.sources
egl_sources = env.ParseSourceList('Makefile.sources', 'LIBEGL_C_FILES')

# libEGL.dll
env['LIBPREFIX'] = 'lib'
env['SHLIBPREFIX'] = 'lib'

egl = env.SharedLibrary(
    target = 'EGL',
    source = egl_sources,
)

Export('egl')
